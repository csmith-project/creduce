#!@perl@
## -*- mode: Perl -*-
##
## Copyright (c) 2012 The University of Utah
## All rights reserved.
##
## This file is distributed under the University of Illinois Open Source
## License.  See the file COPYING for details.

######################################################################
#
# This is a generic Delta debugger that is parameterized by an
# interestingness test implemented as a shell script and a collection
# of transformation operators implemented as Perl modules.
#
####################################################################

## TODO: 
# benchmark the caching code on some large reductions
# use IPC::Run3 instead of system?
# test against LLVM 3.3 when it comes out
# try again to make tmp dir deletion more aggressive; do it only after the wait

use strict;
use warnings;
require 5.10.0;

use FindBin;
use lib $FindBin::Bin, '@perllibdir@';
use Exporter::Lite;
use File::Basename;
use File::Which;
use Getopt::Tabular;
use POSIX;
use Regexp::Common;
use Benchmark::Timer;
use Cwd;
use File::Temp;
use File::Copy;
use Sys::CPU;

use creduce_config qw(PACKAGE_STRING);
use creduce_utils;

######################################################################

my $NPROCS = Sys::CPU::cpu_count();
my $SANITIZE;
my $SANITY;
my $SKIP_FIRST;
my $SAVE_TEMPS;

my @options = (
    ["-n",                    "integer", 1, \$NPROCS, "Set number of creduce processes to run simultaneously", "<N>"],
    ["--sanitize",            "const",   1, \$SANITIZE, "Attempt to obscure details from the original source file"],
    ["--sanity-checks",       "const",   1, \$SANITY,  "Ensure the delta test succeeds before starting each pass"],
    ["--save-temps",          "const",   1, \$SAVE_TEMPS, "Don't delete /tmp/creduce-xxxxxx directories on termination"],
    ["--skip-initial-passes", "const",   1, \$SKIP_FIRST, "Skip initial passes (useful if input is already reduced)"],
    ["--verbose",             "const",   1, \$VERBOSE, "Print debug information"]
);

my $help = creduce_config::PACKAGE_STRING . " - C and C++ program reducer";
my $usage_text = <<USAGE;
usage: creduce [options] test_script.sh file.c
       creduce --help to list options
USAGE

Getopt::Tabular::SetHelp ($help, $usage_text);
Getopt::Tabular::SetOptionPatterns qw|(--)([\w-]+) (-)(\w+)|;
Getopt::Tabular::SetHelpOption("--help");
GetOptions(\@options, \@ARGV) or exit 1;

# if set, cache results of the delta test
#
# FIXME: the caching strategy is not super great since we fork a child
# process before even looking into the cache, but looking first is a
# more invasive change
my $CACHE = 1;

# delete cached results for program sizes this much larger than the
# current size; this heuristic is designed to save RAM while also
# permitting caching of larger results which is useful for some of the
# passes that make (temporary) backwards progress
my $CACHE_PRUNE_SIZE = 5000;

######################################################################

my $orig_file_size;

sub print_pct ($) {
    (my $l) = @_;
    my $pct = 100 - ($l*100.0/$orig_file_size);
    printf "(%.1f %%, $l bytes)\n", $pct;
}

# these are set at startup time and never change
my $test;
my $trial_num = 0;   

my $toreduce;
my $toreduce_orig;
my $toreduce_base;
my $dir_base;
my $suffix;

my $ORIG_DIR;

# global invariant: this filename always points to the best delta
# variant we've seen so far
my $toreduce_best;

######################################################################

my @tmpdirs;

sub make_tmpdir () {
    my $dir = File::Temp::tempdir("creduce-XXXXXX", 
				  $SAVE_TEMPS ? (CLEANUP => 0) : (CLEANUP => 1), 
				  DIR => File::Spec->tmpdir);
    push @tmpdirs, $dir;
    return $dir;
}

sub remove_tmpdirs () {
    return if $SAVE_TEMPS;
    while (my $dir = shift(@tmpdirs)) {
	File::Path::rmtree ($dir, 0, 0);	
    }
}

sub run_test ($) {
    (my $fn) = @_;
    my $res;
    if ($VERBOSE) {
	$res = runit "$test $fn";
    } else {
	$res = runit "$test $fn >/dev/null 2>&1";
    }
    return ($res == 0);
}

sub sanity_check () {
    print "sanity check... " if $VERBOSE;
    my $tmpdir = make_tmpdir();
    chdir $tmpdir or die;
    File::Copy::copy($toreduce_best,$toreduce) or die;
    run_test($toreduce) or die "test (and sanity check) fails";
    print "successful\n" if $VERBOSE;
    chdir $ORIG_DIR or die;
    remove_tmpdirs();
}

my $old_len = 1000000000;
my %cache = ();

sub delta_test ($$$$) {
    (my $method, my $arg, my $state, my $fn) = @_;
    my $prog = read_file($fn);

    my $result;
    $result = $cache{length($prog)}{$prog} if $CACHE;

    if (defined($result)) {
	print "(hit= $result)";
    } else {    
	$result = run_test ($fn);
    }

    return $result ? 1 : 0;
}

sub call_prereq_check ($) {
    (my $method) = @_;
    my $str = $method."::check_prereqs";
    no strict "refs";
    &${str}() or die "prereqs not found for pass $method";
    print "successfully checked prereqs for $method\n" if $VERBOSE;
}

sub call_new ($$$) {
    (my $method,my $fn,my $arg) = @_;    
    my $str = $method."::new";
    no strict "refs";
    return &${str}($fn,$arg);
}

sub call_advance ($$$$) {
    (my $method,my $fn,my $arg,my $state) = @_;    
    my $str = $method."::advance";
    no strict "refs";
    return &${str}($fn,$arg,$state);
}

sub call_transform ($$$$) {
    (my $method,my $fn,my $arg,my $state) = @_;    
    my $str = $method."::transform";
    no strict "refs";
    return &${str}($fn,$arg,$state);
}

my @kids = ();

sub killem () {
    while (scalar(@kids) > 0) {
	my $kidref = shift @kids;
	(my $pid, my $newsh, my $tmpdir, my $tmpfn) = @{$kidref};
	kill ('TERM', $pid);
	waitpid ($pid, 0);	
    }
}

my $good_cnt;
my $bad_cnt;
my $pass_num = 0;
my %method_worked = ();
my %method_failed = ();

# invariant: parallel execution does not escape this function
#
# the parallelization strategy is described here:
#   http://blog.regehr.org/archives/749
sub delta_pass ($) {
    (my $mref) = @_;    
    my $delta_method = ${$mref}{"name"};
    my $delta_arg = ${$mref}{"arg"};
    $good_cnt = 0;
    $bad_cnt = 0;

    die unless (scalar(@kids)==0);

    print "\n" if $VERBOSE;
    print "===< $delta_method :: $delta_arg >===\n";

    my $orig_tmpfn = $toreduce;
    File::Copy::copy($toreduce_best,$orig_tmpfn) or die;
    my $state = call_new ($delta_method,$orig_tmpfn,$delta_arg);

    if ($SANITY) {
	sanity_check();
    }

    my $stopped = 0;
  AGAIN:

    # create child processes until either we've created enough or we get a STOP
    while (!$stopped && scalar(@kids) < $NPROCS) {
	my $tmpdir = make_tmpdir();
	chdir $tmpdir or die;
	my $tmpfn = Cwd::abs_path($orig_tmpfn);
	File::Copy::copy($toreduce_best,$tmpfn) or die;
	(my $delta_res, $state) = call_transform ($delta_method,$tmpfn,$delta_arg,$state);
	die unless ($delta_res == $OK || $delta_res == $STOP);
	if ($delta_res == $STOP) {
	    chdir $ORIG_DIR or die;
	    $stopped = 1;
	} else {
	    my $pid = fork();
	    die unless ($pid >= 0);
	    my @l = ($pid, $state, $tmpdir, $tmpfn);
	    $state = call_advance ($delta_method,$tmpfn,$delta_arg,$state);
	    push @kids, \@l;
	    my $delta_result;
	    # print "[${pass_num} ${delta_method} :: ${delta_arg} s:$good_cnt f:$bad_cnt] " if $VERBOSE;
	    if ($pid==0) {
		$delta_result = delta_test ($delta_method,$delta_arg,$state,$tmpfn);
		exit ($delta_result);
	    }
	    chdir $ORIG_DIR or die;
	}
    }

    # at this point wait if there's anyone to wait for
    if (scalar(@kids)>0) {	
	my $xpid = wait();
	die if ($xpid==-1);
	my $ret = $?;
	my $delta_result = $ret >> 8;	    

	my $found = 0;
	my $pid;
	my $newsh;
	my $tmpdir;
	my $tmpfn;
	for (my $i=0; $i<scalar(@kids); $i++) {
	    my $kidref = $kids[$i];
	    die unless (scalar(@{$kidref})==4);
	    ($pid, $newsh, $tmpdir, $tmpfn) = @{$kidref};
	    if ($xpid==$pid) {
		$found = 1;
		splice (@kids, $i, 1);
		last;
	    }
	}
	die unless $found;

	if ($CACHE) {
	    my $prog = read_file($tmpfn);
	    my $len = length ($prog);
	    $cache{length($prog)}{$prog} = $delta_result;
	    if ($len < $old_len) {
		foreach my $k (keys %cache) {
		    if ($k > ($len + $CACHE_PRUNE_SIZE)) {
			$cache{$k} = ();
		    }
		}
	    }
	    $old_len = $len;
	}

	if ($delta_result) { 
	    # now that the delta test succeeded, this becomes our new
	    # best version--this has to be done in the parent process
	    killem ();
	    $good_cnt++;
	    $method_worked{$delta_method}{$delta_arg}++;
	    $state = $newsh;
	    $stopped = 0;
	    File::Copy::copy($tmpfn,$toreduce_best) or die;
	    print "success " if $VERBOSE;
	    print_pct(-s $toreduce_best);
	} else {
	    print "failure\n" if $VERBOSE;
	    $bad_cnt++;
	    $method_failed{$delta_method}{$delta_arg}++;
	}

	File::Path::rmtree ($tmpdir, 0, 0);	
    }

    # pass termination condition
    if ($stopped && scalar(@kids)==0) {
	remove_tmpdirs();
	return;
    }

    goto AGAIN;
}

sub usage() {
    print $usage_text;
    die;
}

my @all_methods = (
    { "name" => "pass_blank",    "arg" => "0",                                     "first_pass_pri" =>   1, },

    { "name" => "pass_lines",    "arg" => "0",                      "pri" => 410,  "first_pass_pri" =>  20,   "last_pass_pri" => 999, },
    { "name" => "pass_lines",    "arg" => "0",                                     "first_pass_pri" =>  21, },
    { "name" => "pass_lines",    "arg" => "0",                                     "first_pass_pri" =>  22, },
    { "name" => "pass_lines",    "arg" => "1",                      "pri" => 411,  "first_pass_pri" =>  23, },
    { "name" => "pass_lines",    "arg" => "1",                                     "first_pass_pri" =>  24, },
    { "name" => "pass_lines",    "arg" => "1",                                     "first_pass_pri" =>  25, },
    { "name" => "pass_lines",    "arg" => "2",                      "pri" => 412,  "first_pass_pri" =>  27, },
    { "name" => "pass_lines",    "arg" => "2",                                     "first_pass_pri" =>  28, },
    { "name" => "pass_lines",    "arg" => "2",                                     "first_pass_pri" =>  29, },
    { "name" => "pass_lines",    "arg" => "10",                     "pri" => 413,  "first_pass_pri" =>  30, },
    { "name" => "pass_lines",    "arg" => "10",                                    "first_pass_pri" =>  31, },
    { "name" => "pass_lines",    "arg" => "10",                                    "first_pass_pri" =>  32, },

    { "name" => "pass_crc",      "arg" => "",                                      "first_pass_pri" => 110, },
    { "name" => "pass_ternary",  "arg" => "b",                      "pri" => 104,  },
    { "name" => "pass_ternary",  "arg" => "c",                      "pri" => 105,  },
    { "name" => "pass_balanced", "arg" => "curly",                  "pri" => 110,  "first_pass_pri" =>  35, },
    { "name" => "pass_balanced", "arg" => "curly2",                 "pri" => 111,  "first_pass_pri" =>  36, },
    { "name" => "pass_balanced", "arg" => "curly3",                 "pri" => 112,  "first_pass_pri" =>  37, },
    { "name" => "pass_balanced", "arg" => "parens",                 "pri" => 113,  },
    { "name" => "pass_balanced", "arg" => "angles",                 "pri" => 114,  },
    { "name" => "pass_balanced", "arg" => "curly-only",             "pri" => 150,  },
    { "name" => "pass_balanced", "arg" => "parens-only",            "pri" => 151,  },
    { "name" => "pass_balanced", "arg" => "angles-only",            "pri" => 152,  },
    { "name" => "pass_clang",    "arg" => "remove-namespace",       "pri" => 200,  },
    { "name" => "pass_clang",    "arg" => "aggregate-to-scalar",    "pri" => 201,  },
   #{ "name" => "pass_clang",    "arg" => "binop-simplification",   "pri" => 201,  },
    { "name" => "pass_clang",    "arg" => "local-to-global",        "pri" => 202,  },
    { "name" => "pass_clang",    "arg" => "param-to-global",        "pri" => 203,  },
    { "name" => "pass_clang",    "arg" => "param-to-local",         "pri" => 204,  },
    { "name" => "pass_clang",    "arg" => "remove-nested-function", "pri" => 205,  },
    { "name" => "pass_clang",    "arg" => "rename-fun",                            "last_pass_pri" => 207,  },
    { "name" => "pass_clang",    "arg" => "union-to-struct",        "pri" => 208,  },
    { "name" => "pass_clang",    "arg" => "rename-param",                          "last_pass_pri" => 209,  },
    { "name" => "pass_clang",    "arg" => "rename-var",                            "last_pass_pri" => 210,  },
    { "name" => "pass_clang",    "arg" => "rename-class",                          "last_pass_pri" => 211,  },
    { "name" => "pass_clang",    "arg" => "return-void",            "pri" => 212,  },
    { "name" => "pass_clang",    "arg" => "simple-inliner",         "pri" => 213,  },
    { "name" => "pass_clang",    "arg" => "reduce-pointer-level",   "pri" => 214,  },
    { "name" => "pass_clang",    "arg" => "lift-assignment-expr",   "pri" => 215,  },
    { "name" => "pass_clang",    "arg" => "copy-propagation",       "pri" => 216,  },
    { "name" => "pass_clang",    "arg" => "callexpr-to-value",      "pri" => 217,  "first_pass_pri" => 49, },
    { "name" => "pass_clang",    "arg" => "replace-callexpr",       "pri" => 218,  "first_pass_pri" => 50, },
    { "name" => "pass_clang",    "arg" => "simplify-callexpr",      "pri" => 219,  "first_pass_pri" => 51, },
    { "name" => "pass_clang",    "arg" => "remove-unused-function", "pri" => 220,  "first_pass_pri" => 33, },
    { "name" => "pass_clang",    "arg" => "remove-unused-enum-member", "pri" => 221, "first_pass_pri" => 51, },
    { "name" => "pass_clang",    "arg" => "remove-enum-member-value", "pri" => 222, "first_pass_pri" => 52, },
    { "name" => "pass_clang",    "arg" => "remove-unused-var",      "pri" => 223,  "first_pass_pri" => 53, },
    { "name" => "pass_clang",    "arg" => "simplify-if",            "pri" => 224,  },
    { "name" => "pass_clang",    "arg" => "reduce-array-dim",       "pri" => 225,  },
    { "name" => "pass_clang",    "arg" => "reduce-array-size",      "pri" => 226,  },
    { "name" => "pass_clang",    "arg" => "move-function-body",     "pri" => 227,  },
    { "name" => "pass_clang",    "arg" => "simplify-comma-expr",    "pri" => 228,  },
    { "name" => "pass_clang",    "arg" => "simplify-dependent-typedef",   "pri" => 229,  },
    { "name" => "pass_clang",    "arg" => "replace-simple-typedef", "pri" => 230,  },
    { "name" => "pass_clang",    "arg" => "remove-unused-field",    "pri" => 231,  },
    { "name" => "pass_clang",    "arg" => "reduce-class-template-param",  "pri" => 232,  },
    { "name" => "pass_clang",    "arg" => "remove-trivial-base-template", "pri" => 233,  },
    { "name" => "pass_clang",    "arg" => "class-template-to-class",      "pri" => 234,  },
    { "name" => "pass_clang",    "arg" => "remove-base-class",      "pri" => 235,  },
    { "name" => "pass_clang",    "arg" => "replace-derived-class",  "pri" => 236,  },
    { "name" => "pass_clang",    "arg" => "remove-unresolved-base", "pri" => 237,  },
    { "name" => "pass_clang",    "arg" => "remove-ctor-initializer","pri" => 238,  },
    { "name" => "pass_clang",    "arg" => "empty-struct-to-int",    "pri" => 239,  },
    { "name" => "pass_clang",    "arg" => "remove-pointer",         "pri" => 240,  },
    { "name" => "pass_clang",    "arg" => "remove-pointer-pairs",   "pri" => 241,  },
    { "name" => "pass_clang",    "arg" => "remove-array",           "pri" => 242,  },
    { "name" => "pass_clang",    "arg" => "remove-addr-taken",      "pri" => 243,  },
    { "name" => "pass_clang",    "arg" => "simplify-struct",        "pri" => 244,  },
    { "name" => "pass_clang",    "arg" => "replace-undefined-function",   "pri" => 245,  },
    { "name" => "pass_clang",    "arg" => "replace-array-index-var",      "pri" => 246,  },
    { "name" => "pass_clang",    "arg" => "combine-global-var",                    "last_pass_pri" => 990, },
    { "name" => "pass_clang",    "arg" => "combine-local-var",                     "last_pass_pri" => 991, },
    { "name" => "pass_clang",    "arg" => "simplify-struct-union-decl",            "last_pass_pri" => 992, },
    { "name" => "pass_clang",    "arg" => "move-global-var",                       "last_pass_pri" => 993, },
    { "name" => "pass_clang",    "arg" => "unify-function-decl",                   "last_pass_pri" => 994, },
    { "name" => "pass_peep",     "arg" => "a",                      "pri" => 500,  },
    { "name" => "pass_peep",     "arg" => "b",                      "pri" => 501,  },
    { "name" => "pass_ints",     "arg" => "a",                      "pri" => 600,  },
    { "name" => "pass_ints",     "arg" => "b",                      "pri" => 601,  },
    { "name" => "pass_ints",     "arg" => "c",                      "pri" => 602,  },
    { "name" => "pass_ints",     "arg" => "d",                      "pri" => 603,  },
    { "name" => "pass_ints",     "arg" => "e",                      "pri" => 603,  },
    { "name" => "pass_indent",   "arg" => "regular",                "pri" => 1000, },
    { "name" => "pass_indent",   "arg" => "final",                                 "last_pass_pri" => 1000, },
    );

sub pass_iterator ($) {
    (my $which) = @_;
    my $last_pri = -999999;
    my $done = 0;

    return sub {
	my $next;
	foreach my $href (@all_methods) {
	    my %h = %{$href};
	    my $p = $h{$which};
	    next unless defined($p);
	    next unless ($p>$last_pri);
	    if (!defined($next)) {
		$next = $href;
		next;
	    }
	    my %nh = %{$next};
	    if ($p < $nh{$which}) {
		$next = $href;
		next
	    }
	}
	if (defined($next)) {
	    my %nh = %{$next};
	    $last_pri = $nh{$which};
	}
        return $next;
    };
}

my %file_attr_to_error = (
    e => "not found",
    f => "is not a plain file",
    r => "is not readable",
    w => "is not writable",
    x => "is not executable",
);

sub check_file_attributes($$$) {
    my ($prefix, $file, $attrs) = @_;
    for my $attr (split //, $attrs) {
        if (eval '! -' . $attr . ' $file') {
            print "$prefix '$file' $file_attr_to_error{$attr}\n";
            usage();
        }
    }
}

############################### main #################################

my $timer = Benchmark::Timer->new();
$timer->start();

my %prereqs_checked;
foreach my $mref (@all_methods) {
    my %method = %{$mref};
    my $mname = $method{"name"};
    die unless defined ($mname);
    next if defined ($prereqs_checked{$mname});
    $prereqs_checked{$mname} = 1;
    eval "require $mname";
    call_prereq_check($mname);
}
print "\n" if $VERBOSE;

$test = Cwd::abs_path(shift @ARGV);
usage unless defined($test);
check_file_attributes("test script", $test, "efrx");

$toreduce = shift @ARGV;
usage unless defined($toreduce);
check_file_attributes("file", $toreduce, "efrw");

print "===< $$ >===\n";
print "running $NPROCS interestingness test(s) in parallel\n";

# Put scratch files ($toreduce_best, $toreduce_orig) in the current
# working directory.
($toreduce_base, $dir_base, $suffix) = fileparse($toreduce, '\.[^.]*');

$ORIG_DIR = getcwd();

# absolute path so we can refer to this file from temporary working
# dirs
$toreduce_best  = Cwd::abs_path("$toreduce_base.best");
$toreduce_orig = "$toreduce_base.orig";

File::Copy::copy($toreduce,$toreduce_orig) or die;
File::Copy::copy($toreduce,$toreduce_best) or die;

my $file_size = -s $toreduce;
$orig_file_size = $file_size;

# unconditionally do this just once since otherwise output is
# confusing when the initial test fails
sanity_check();

# some passes we run first since they often make good headway quickliy
if (not $SKIP_FIRST) {
    print "INITIAL PASSES\n" if $VERBOSE;
    my $next = pass_iterator("first_pass_pri");
    while (my $item = $next->()) {
        delta_pass ($item);
    }
}

# iterate to global fixpoint
print "MAIN PASSES\n" if $VERBOSE;
$file_size = -s $toreduce;

while (1) {
    my $next = pass_iterator("pri");
    while (my $item = $next->()) {
	delta_pass ($item);
    }
    $pass_num++;
    my $s = -s $toreduce_best;
    print "Termination check: size was $file_size; now $s\n";
    last if ($s >= $file_size);
    $file_size = $s;
}

# some passes we run last since they work best as cleanup
print "CLEANUP PASS\n" if $VERBOSE;
{
    my $next = pass_iterator("last_pass_pri");
    while (my $item = $next->()) {
	delta_pass ($item);
    }
}

print "===================== done ====================\n";

print "\n";
print "pass statistics:\n";
foreach my $mref (sort @all_methods) {
    my $method = ${$mref}{"name"};
    my $arg = ${$mref}{"arg"};
    my $w = $method_worked{$method}{$arg};
    $w=0 unless defined($w);
    my $f = $method_failed{$method}{$arg};
    $f=0 unless defined($f);
    print "  method $method :: $arg worked $w times and failed $f times\n";
}

print "\n";

# this should be the only time we touch the original file
File::Copy::copy($toreduce_best,$toreduce) or die;

print "reduced test case:\n\n";
open INF, "<$toreduce" or die;
while (<INF>) {
    print;
}
close INF;
print "\n";

$timer->stop();
my $time = int($timer->result());
print "elapsed time: $time seconds\n";

######################################################################
